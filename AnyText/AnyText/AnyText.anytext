grammar AnyText ( anytext )
root Grammar
imports https://github.com/NMFCode/NMF/AnyText

comment '//'
comment '/*' to '*/'

Grammar:
  'grammar' Name=ID ( '(' LanguageId=LanguageId ')' )? <nl>
  'root' StartRule=[ClassRule] <nl>
  Imports+=MetamodelImport* <nl>
  Comments+=CommentRule <nl>* <nl>
  Rules+=Rule <nl>+;

CommentRule:
  MultilineCommentRule | SinglelineCommentRule;

SinglelineCommentRule:
  'comment' Start=Keyword;

MultilineCommentRule:
  'comment' Start=Keyword 'to' End=Keyword;

MetamodelImport:
  'imports' ( Prefix=ID 'from' )? File=Uri <nl>;

Rule:
  ClassRule | DataRule | FragmentRule | ParanthesisRule | EnumRule;

ClassRule:
  InheritanceRule | ModelRule;

InheritanceRule:
  Name=ID RuleTypeFragment ':' <nl>
  <ind> Subtypes+=[ClassRule] ( '|' Subtypes+=[ClassRule] )+ <nsp> ';' <unind> <nl>;

ModelRule:
  Name=ID RuleTypeFragment ':' <nl>
  <ind> Expression=ParserExpression FormattingInstructions+=FormattingInstruction* <nsp> ';' <unind> <nl>;

DataRule:
  'terminal' Name=ID RuleTypeFragment ':' <nl>
  <ind> Regex=Regex ( 'surround' 'with' SurroundCharacter=Char )? ( 'escape' EscapeRules+=EscapeRule ( ',' EscapeRules+=EscapeRule )* )? <nsp> ';' <unind> <nl>;

EscapeRule:
  Character=Char 'as' Escape=Keyword;

FragmentRule:
  'fragment' Name=ID 'processes' ( Prefix=ID <nsp> '.' <nsp> )? TypeName=ID <nsp> ':' <nl>
  <ind> Expression=ParserExpression FormattingInstructions+=FormattingInstruction* <nsp> ';' <unind> <nl>;

ParanthesisRule:
  'parantheses' Name=ID ':' <nl>
  <ind> OpeningParanthesis=KeywordExpression FormattingInstructionsAfterOpening+=FormattingInstruction* InnerRule=[ClassRule] FormattingInstructionsInner+=FormattingInstruction* ClosingParanthesis=KeywordExpression FormattingInstructionsAfterClosing+=FormattingInstruction* <nsp> ';' <unind> <nl>;

EnumRule:
  'enum' Name=ID RuleTypeFragment ':' <nl>
  <ind> Literals+=LiteralRule+ <nsp> ';' <unind> <nl>;

LiteralRule:
  Literal=ID '=>' Keyword=FormattedSequenceOrExpression <nl>;

fragment RuleTypeFragment processes Rule:
  ( 'returns' ( Prefix=ID <nsp> '.' <nsp> )? TypeName=ID )? <nsp>;

fragment FormattingInstructionFragment processes UnaryParserExpression:
  FormattingInstructions+=FormattingInstruction*;

enum FormattingInstruction:
  Newline => '<nl>' <nl>

  Indent => '<ind>' <ind>
    Unindent => '<unind>' <unind>
  AvoidSpace => '<nsp>'
  ForbidSpace => '<!nsp>';

ParserExpression:
  ChoiceExpression | ExpressionOrSequence;

ExpressionOrSequence returns ParserExpression:
  SequenceExpression | ConjunctiveParserExpression;

ConjunctiveParserExpression returns ParserExpression:
  PlusExpression | StarExpression | MaybeExpression | BasicParserExpression;

BasicParserExpression returns ParserExpression:
  NegativeLookaheadExpression | PositiveLookaheadExpression | KeywordExpression | ReferenceExpression | AssignExpression | AddAssignExpression | ExistsAssignExpression | RuleExpression | ParanthesisExpression;

parantheses ParanthesisExpression :
  '(' ParserExpression ')';

SequenceExpression:
  InnerExpressions+=FormattedExpression InnerExpressions+=FormattedExpression+;

FormattedExpression:
  Expression=ConjunctiveParserExpression FormattingInstructions+=FormattingInstruction*;

FormattedSequenceOrExpression returns FormattedExpression:
  Expression=ExpressionOrSequence FormattingInstructions+=FormattingInstruction*;

PlusExpression:
  Inner=BasicParserExpression FormattingInstructionFragment <nsp> '+';

StarExpression:
  Inner=BasicParserExpression FormattingInstructionFragment <nsp> '*';

MaybeExpression:
  Inner=BasicParserExpression FormattingInstructionFragment <nsp> '?';

KeywordExpression:
  Keyword=Keyword;

ChoiceExpression:
  ( Alternatives+=FormattedSequenceOrExpression '|' )+ Alternatives+=FormattedSequenceOrExpression;

AssignExpression:
  Feature=IdOrContextRef <nsp> '=' <nsp> Assigned=BasicParserExpression;

AddAssignExpression:
  Feature=IdOrContextRef <nsp> '+=' <nsp> Assigned=BasicParserExpression;

ExistsAssignExpression:
  Feature=IdOrContextRef <nsp> '?=' <nsp> Assigned=BasicParserExpression;

NegativeLookaheadExpression:
  '!' <nsp> Inner=BasicParserExpression;

PositiveLookaheadExpression:
  '&' <nsp> Inner=BasicParserExpression;

RuleExpression:
  Rule=[Rule] !'=' !'+=' !'?=';

ReferenceExpression:
  '[' <nsp> ReferencedRule=[Rule] <nsp> ( ':' Format=[DataRule] )? ']';

terminal IdOrContextRef:
  /(context\.)?[a-zA-Z]\w*/;

terminal ID:
  /[a-zA-Z]\w*/;

terminal LanguageId:
  /[a-zA-Z]([\w\-]*\w)?/;

terminal Keyword:
  /(\\\\|\\'|[^'\\])+/ surround with ' escape \ as '\\\\' , ' as '\\\'';

terminal Regex:
  /(\\\/|[^\/])*/ surround with / escape / as '\\/';

terminal Uri:
  /.+/;

terminal Char:
  /\S+/;

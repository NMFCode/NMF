grammar AnyText ( anytext )
root Grammar
imports https://github.com/NMFCode/NMF/AnyText

comment '//'
comment '/*' to '*/'

Grammar:
  'grammar' Name=ID ( '(' LanguageId=LanguageId ')' )? <nl>
  'root' StartRule=[ClassRule] <nl>
  Imports+=MetamodelImport* <nl>
  Comments+=CommentRule <nl>* <nl>
  Rules+=Rule <nl>+;

CommentRule:
  MultilineCommentRule | SinglelineCommentRule;

SinglelineCommentRule:
  'comment' Start=Keyword;

MultilineCommentRule:
  'comment' Start=Keyword 'to' End=Keyword;

MetamodelImport:
  'imports' ( Prefix=ID 'from' )? File=Uri <nl>;

Rule:
  ClassRule | DataRule | FragmentRule | ParanthesisRule | EnumRule;

ClassRule:
  InheritanceRule | ModelRule;

InheritanceRule:
  Name=ID RuleTypeFragment ':' <nl>
  <ind> Subtypes+=[ClassRule] ( '|' Subtypes+=[ClassRule] )+ <nsp> ';' <unind> <nl>;

ModelRule:
  Name=ID RuleTypeFragment ':' <nl>
  <ind> Expression=ParserExpression <nsp> ';' <unind> <nl>;

DataRule:
  'terminal' Name=ID RuleTypeFragment ':' <nl>
  <ind> Regex=Regex ( 'surround' 'with' SurroundCharacter=Char )? ( 'escape' EscapeRules+=EscapeRule ( ',' EscapeRules+=EscapeRule )* )? <nsp> ';' <unind> <nl>;

EscapeRule:
  Character=Char 'as' Escape=Keyword;

FragmentRule:
  'fragment' Name=ID 'processes' ( Prefix=ID <nsp> '.' <nsp> )? TypeName=ID <nsp> ':' <nl>
  <ind> Expression=ParserExpression <nsp> ';' <unind> <nl>;

ParanthesisRule:
  'parantheses' Name=ID ':' <nl>
  <ind> OpeningParanthesis=KeywordExpression InnerRule=[ClassRule] ClosingParanthesis=KeywordExpression <nsp> ';' <unind> <nl>;

EnumRule:
  'enum' Name=ID RuleTypeFragment ':' <nl>
  <ind> Literals+=LiteralRule+ <nsp> ';' <unind> <nl>;

LiteralRule:
  Literal=ID '=>' Keyword=ParserExpression <nl>;

fragment RuleTypeFragment processes Rule:
  ( 'returns' ( Prefix=ID <nsp> '.' <nsp> )? TypeName=ID )? <nsp>;

fragment FormattingInstructionFragment processes ParserExpression:
  FormattingInstructions+=FormattingInstruction*;

enum FormattingInstruction:
  Newline => '<nl>' <nl>

  Indent => '<ind>' <ind>
    Unindent => '<unind>' <unind>
  AvoidSpace => '<nsp>'
  ForbidSpace => '<!nsp>';

ParserExpression:
  SequenceExpression | ChoiceExpression | ConjunctiveParserExpression;

ConjunctiveParserExpression returns ParserExpression:
  PlusExpression | StarExpression | MaybeExpression | BasicParserExpression;

BasicParserExpression returns ParserExpression:
  NegativeLookaheadExpression | KeywordExpression | ReferenceExpression | AssignExpression | AddAssignExpression | ExistsAssignExpression | RuleExpression | ParanthesisExpression;

parantheses ParanthesisExpression :
  '(' ParserExpression ')';

SequenceExpression:
  InnerExpressions+=ConjunctiveParserExpression InnerExpressions+=ConjunctiveParserExpression+;

PlusExpression:
  Inner=BasicParserExpression <nsp> '+' FormattingInstructionFragment;

StarExpression:
  Inner=BasicParserExpression <nsp> '*' FormattingInstructionFragment;

MaybeExpression:
  Inner=BasicParserExpression <nsp> '?' FormattingInstructionFragment;

KeywordExpression:
  Keyword=Keyword FormattingInstructionFragment;

ChoiceExpression:
  ( Alternatives+=ConjunctiveParserExpression '|' )+ Alternatives+=ConjunctiveParserExpression;

AssignExpression:
  Feature=IdOrContextRef <nsp> '=' <nsp> Assigned=BasicParserExpression FormattingInstructionFragment;

AddAssignExpression:
  Feature=IdOrContextRef <nsp> '+=' <nsp> Assigned=BasicParserExpression FormattingInstructionFragment;

ExistsAssignExpression:
  Feature=IdOrContextRef <nsp> '?=' <nsp> Assigned=BasicParserExpression FormattingInstructionFragment;

NegativeLookaheadExpression:
  '!' <nsp> Inner=BasicParserExpression;

RuleExpression:
  Rule=[Rule] FormattingInstructionFragment !'=' !'+=' !'?=';

ReferenceExpression:
  '[' <nsp> ReferencedRule=[Rule] <nsp> ( ':' Format=[DataRule] )? ']' FormattingInstructionFragment;

terminal IdOrContextRef:
  /(context\.)?[a-zA-Z]\w*/;

terminal ID:
  /[a-zA-Z]\w*/;

terminal LanguageId:
  /[a-zA-Z]([\w\-]*\w)?/;

terminal Keyword:
  /(\\\\|\\'|[^'\\])+/ surround with ' escape \ as '\\\\' , ' as '\\\'';

terminal Regex:
  /(\\\/|[^\/])*/ surround with / escape / as '\\/';

terminal Uri:
  /.+/;

terminal Char:
  /\S+/;

